#
# Build JNI Bindings
#

FIND_PACKAGE(JNI REQUIRED)

SET(PHYSX_SOURCE_DIR ${PHYSX_ROOT_DIR}/source)
SET(PHYSX_JNIBINDINGS_SOURCE_DIR ${PHYSX_SOURCE_DIR}/webidlbindings/src)

SET(PHYSX_JNIBINDINGS_COMPILE_DEFS
	# Common to all configurations
    ${PHYSX_WINDOWS_COMPILE_DEFS};${PXPHYSX_LIBTYPE_DEFS};${PHYSX_GPU_SHARED_LIB_NAME_DEF};${PHYSX_LIBTYPE_DEFS};${PHYSXGPU_LIBTYPE_DEFS}

    $<$<CONFIG:debug>:${PHYSX_WINDOWS_DEBUG_COMPILE_DEFS};>
    $<$<CONFIG:checked>:${PHYSX_WINDOWS_CHECKED_COMPILE_DEFS};>
    $<$<CONFIG:profile>:${PHYSX_WINDOWS_PROFILE_COMPILE_DEFS};>
    $<$<CONFIG:release>:${PHYSX_WINDOWS_RELEASE_COMPILE_DEFS};>
)

SET(PHYSX_JNIBINDINGS_LIBTYPE SHARED)

IF(NV_USE_GAMEWORKS_OUTPUT_DIRS AND PHYSX_JNIBINDINGS_LIBTYPE STREQUAL "STATIC")
	SET(PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_DEBUG "PhysXJniBindings_static${CMAKE_DEBUG_POSTFIX}")
	SET(PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_CHECKED "PhysXJniBindings_static${CMAKE_CHECKED_POSTFIX}")
	SET(PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_PROFILE "PhysXJniBindings_static${CMAKE_PROFILE_POSTFIX}")
	SET(PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_RELEASE "PhysXJniBindings_static${CMAKE_RELEASE_POSTFIX}")
ELSE()
	SET(PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_DEBUG "PhysXJniBindings${CMAKE_DEBUG_POSTFIX}")
	SET(PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_CHECKED "PhysXJniBindings${CMAKE_CHECKED_POSTFIX}")
	SET(PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_PROFILE "PhysXJniBindings${CMAKE_PROFILE_POSTFIX}")
	SET(PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_RELEASE "PhysXJniBindings${CMAKE_RELEASE_POSTFIX}")
ENDIF()

IF(PHYSX_JNIBINDINGS_LIBTYPE STREQUAL "SHARED")
	INSTALL(FILES $<TARGET_PDB_FILE:PhysXJniBindings> 
		DESTINATION $<$<CONFIG:debug>:${PX_ROOT_LIB_DIR}/debug>$<$<CONFIG:release>:${PX_ROOT_LIB_DIR}/release>$<$<CONFIG:checked>:${PX_ROOT_LIB_DIR}/checked>$<$<CONFIG:profile>:${PX_ROOT_LIB_DIR}/profile> OPTIONAL)
ELSE()	
	INSTALL(FILES ${PHYSX_ROOT_DIR}/$<$<CONFIG:debug>:${PX_ROOT_LIB_DIR}/debug>$<$<CONFIG:release>:${PX_ROOT_LIB_DIR}/release>$<$<CONFIG:checked>:${PX_ROOT_LIB_DIR}/checked>$<$<CONFIG:profile>:${PX_ROOT_LIB_DIR}/profile>/$<$<CONFIG:debug>:${PHYSXJNIBINDINGS_COMPILE_PDB_NAME_DEBUG}>$<$<CONFIG:checked>:${PHYSXJNIBINDINGS_COMPILE_PDB_NAME_CHECKED}>$<$<CONFIG:profile>:${PHYSXJNIBINDINGS_COMPILE_PDB_NAME_PROFILE}>$<$<CONFIG:release>:${PHYSXJNIBINDINGS_COMPILE_PDB_NAME_RELEASE}>.pdb
		DESTINATION $<$<CONFIG:debug>:${PX_ROOT_LIB_DIR}/debug>$<$<CONFIG:release>:${PX_ROOT_LIB_DIR}/release>$<$<CONFIG:checked>:${PX_ROOT_LIB_DIR}/checked>$<$<CONFIG:profile>:${PX_ROOT_LIB_DIR}/profile> OPTIONAL)
ENDIF()

SET(PHYSX_JNIBINDINGS_LINK_FLAGS "/MAP")
IF(NOT PX_GENERATE_STATIC_LIBRARIES)
	SET(PHYSX_JNIBINDINGS_LINK_FLAGS_DEBUG "/DELAYLOAD:PhysXFoundation${CMAKE_DEBUG_POSTFIX}.dll")
	SET(PHYSX_JNIBINDINGS_LINK_FLAGS_CHECKED "/DELAYLOAD:PhysXFoundation${CMAKE_CHECKED_POSTFIX}.dll")
	SET(PHYSX_JNIBINDINGS_LINK_FLAGS_PROFILE "/DELAYLOAD:PhysXFoundation${CMAKE_PROFILE_POSTFIX}.dll")
	SET(PHYSX_JNIBINDINGS_LINK_FLAGS_RELEASE "/DELAYLOAD:PhysXFoundation${CMAKE_RELEASE_POSTFIX}.dll")
ENDIF()



SET(PHYSX_JNIBINDINGS_HEADERS

)
SOURCE_GROUP(include FILES ${PHYSX_JNIBINDINGS_HEADERS})

SET(PHYSX_JNIBINDINGS_SOURCE
	${PHYSX_JNIBINDINGS_SOURCE_DIR}/common/vehicle/Base.cpp
	${PHYSX_JNIBINDINGS_SOURCE_DIR}/common/vehicle/DirectDrivetrain.cpp
	${PHYSX_JNIBINDINGS_SOURCE_DIR}/common/vehicle/EngineDrivetrain.cpp
	${PHYSX_JNIBINDINGS_SOURCE_DIR}/common/vehicle/PhysXIntegration.cpp

	${PHYSX_JNIBINDINGS_SOURCE_DIR}/jni/PhysXJni.cpp
)
SOURCE_GROUP(src FILES ${PHYSX_JNIBINDINGS_SOURCE})

ADD_LIBRARY(PhysXJniBindings ${PHYSX_JNIBINDINGS_LIBTYPE}
	${PHYSX_JNIBINDINGS_SOURCE}
	${PHYSX_JNIBINDINGS_HEADERS}
)

TARGET_INCLUDE_DIRECTORIES(PhysXJniBindings 
	PRIVATE ${JNI_INCLUDE_DIRS}
	PRIVATE ${PHYSX_JNIBINDINGS_PLATFORM_INCLUDES}

	PRIVATE ${PHYSX_ROOT_DIR}/include
	PRIVATE ${PHYSX_ROOT_DIR}/pvdruntime/include
	PRIVATE ${PHYSX_SOURCE_DIR}/pvd/include
)

TARGET_LINK_LIBRARIES(PhysXJniBindings 
	PUBLIC PhysXCommon
	PUBLIC PhysXFoundation
	PUBLIC PhysXExtensions
	PUBLIC PhysXCooking
	PUBLIC PhysXVehicle2
	PUBLIC PhysXTask
	PUBLIC PhysXCharacterKinematic
	PUBLIC PhysXPvdSDK
)

TARGET_COMPILE_DEFINITIONS(PhysXJniBindings 
	PRIVATE ${PHYSX_JNIBINDINGS_COMPILE_DEFS}
)

SET_TARGET_PROPERTIES(PhysXJniBindings PROPERTIES
	OUTPUT_NAME PhysXJniBindings
	LINK_FLAGS ${PHYSX_JNIBINDINGS_LINK_FLAGS}
	LINK_FLAGS_DEBUG ${PHYSX_JNIBINDINGS_LINK_FLAGS_DEBUG}
	LINK_FLAGS_CHECKED ${PHYSX_JNIBINDINGS_LINK_FLAGS_CHECKED}
	LINK_FLAGS_PROFILE ${PHYSX_JNIBINDINGS_LINK_FLAGS_PROFILE}
	LINK_FLAGS_RELEASE ${PHYSX_JNIBINDINGS_LINK_FLAGS_RELEASE}
)
	

IF(NV_USE_GAMEWORKS_OUTPUT_DIRS AND PHYSX_JNIBINDINGS_LIBTYPE STREQUAL "STATIC")	
	SET_TARGET_PROPERTIES(PhysXJniBindings PROPERTIES 
		ARCHIVE_OUTPUT_NAME_DEBUG "PhysXJniBindings_static"
		ARCHIVE_OUTPUT_NAME_CHECKED "PhysXJniBindings_static"
		ARCHIVE_OUTPUT_NAME_PROFILE "PhysXJniBindings_static"
		ARCHIVE_OUTPUT_NAME_RELEASE "PhysXJniBindings_static"
	)
ENDIF()

IF(PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_DEBUG)
	SET_TARGET_PROPERTIES(PhysXJniBindings PROPERTIES 
		COMPILE_PDB_NAME_DEBUG ${PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_DEBUG}
		COMPILE_PDB_NAME_CHECKED ${PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_CHECKED}
		COMPILE_PDB_NAME_PROFILE ${PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_PROFILE}
		COMPILE_PDB_NAME_RELEASE ${PHYSX_JNIBINDINGS_COMPILE_PDB_NAME_RELEASE}
	)
ENDIF()

# enable -fPIC so we can link static libs with the editor
SET_TARGET_PROPERTIES(PhysXJniBindings PROPERTIES POSITION_INDEPENDENT_CODE TRUE)

# disable exception handling in generated glue code
TARGET_COMPILE_OPTIONS(PhysXJniBindings PRIVATE /EHsc)